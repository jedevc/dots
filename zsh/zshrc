# history
setopt INC_APPEND_HISTORY HIST_IGNORE_DUPS
HISTFILE=~/.histfile
HISTSIZE=2000
SAVEHIST=1000000000

# keybindings
set -o vi
bindkey -v '^?' backward-delete-char
bindkey -v '^H' backward-delete-char
bindkey -v '^[[3~' delete-char

# history search
autoload -U history-search-end
zle -N history-beginning-search-backward-end history-search-end
zle -N history-beginning-search-forward-end history-search-end
bindkey '^[[A' history-beginning-search-backward-end
bindkey '^[[B' history-beginning-search-forward-end
bindkey -a 'k' history-beginning-search-backward-end
bindkey -a 'j' history-beginning-search-forward-end

# fg widget
function fg_function { fg }
zle -N fg_widget fg_function
bindkey '^F' fg_widget

# pushd
setopt AUTO_PUSHD PUSHD_IGNORE_DUPS PUSHD_MINUS PUSHD_TO_HOME

# autocompletion
autoload -Uz compinit
compinit
zstyle ':completion:*' menu select
zstyle ':completion:*' rehash true
setopt COMPLETE_ALIASES

# colors
autoload -U colors && colors
alias ls='ls --color=auto'
alias grep='grep --color=auto'

# aliases
alias e='emacsclient -c'

# prompt
PROMPT="%F{blue}%1~%f %% "
if [[ $DISPLAY ]]; then
	POWERLEVEL9K_MODE='awesome-fontconfig'
	POWERLEVEL9K_LEFT_PROMPT_ELEMENTS=(dir)
	POWERLEVEL9K_RIGHT_PROMPT_ELEMENTS=(virtualenv vcs)
	POWERLEVEL9K_SHORTEN_STRATEGY=truncate_to_last

	# find powerlevel9k
	if [[ -f /usr/share/zsh-theme-powerlevel9k/powerlevel9k.zsh-theme ]]; then
		source /usr/share/zsh-theme-powerlevel9k/powerlevel9k.zsh-theme
	elif [[ -f /usr/share/powerlevel9k/powerlevel9k.zsh-theme ]]; then
		source /usr/share/powerlevel9k/powerlevel9k.zsh-theme
	fi
fi

# find zsh-syntax-highlighting
if [[ -f /usr/share/zsh/plugins/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh ]]; then
	source /usr/share/zsh/plugins/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh
elif [[ -f /usr/share/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh ]]; then
	source /usr/share/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh
fi

# find zsh-autosuggestions
if [[ -f /usr/share/zsh/plugins/zsh-autosuggestions/zsh-autosuggestions.zsh ]]; then
	source /usr/share/zsh/plugins/zsh-autosuggestions/zsh-autosuggestions.zsh
	ZSH_AUTOSUGGEST_BUFFER_MAX_SIZE=40
fi

# window title (borrowed from ArchWiki)
autoload -Uz add-zsh-hook
function xterm_title_precmd () {
	print -Pn '\e]2;%n@%m %1~\a'
}
function xterm_title_preexec () {
	print -Pn '\e]2;%n@%m %1~ %# '
	print -n "${(q)1}\a"
}
if [[ "$TERM" == (screen*|xterm*|rxvt*) ]]; then
	add-zsh-hook -Uz precmd xterm_title_precmd
	add-zsh-hook -Uz preexec xterm_title_preexec
fi
